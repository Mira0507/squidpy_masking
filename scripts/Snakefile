import os
import pandas as pd

##### for interactive debugging
# import yaml
# with open("config/config.yaml", 'r') as stream:
# config = yaml.safe_load(stream)


# Specify path to config file
configfile: 'config/config.yaml'

# Prep sampletable
sample = pd.read_table(config['sampletable']).set_index('samplename', drop=False)

# Wrapper script directory
wrapper_dir = config['wrapper_dir']

rule all:
    input:
        expand("{outputdir}/{name}/converted.ome.tif", outputdir=config['outdir'], name=list(sample.index))

rule convert:
    """
    This rule converts VSI images to OME-TIFF format 
    using the bftools package
    """
    input: 
        vsi = lambda wildcards: sample.loc[wildcards.name, 'input_vsi'],
        rmd = f"{wrapper_dir}/image_conversion.Rmd"
    output: 
        tif = "{outputdir}/{name}/converted.ome.tif",
        html = "{outputdir}/{name}/image_conversion.html"
    threads: 1
    resources:
        mem_mb=1024 * 40,
        disk_mb=1024 * 20,
        runtime=60 * 8,
        tmpdir=config['outdir']
    params:
        outdir = config['outdir'],
        series = config['ser'],
        pyramidal = config['pyramidal']
    shell:
        """
        Rscript -e 'params = list(
            vsi = "{input.vsi}",
            output_dir = "{params.outdir}",
            tif = "{output.tif}",
            input_meta_out = "{params.outdir}/{wildcards.name}/input_metadata.txt",
            output_meta_out = "{params.outdir}/{wildcards.name}/output_metadata.txt",
            ser = "{params.series}",
            to_pyramidal = "{params.pyramidal}");
        rmarkdown::render("{input.rmd}",
            params = params,
            intermediates_dir = "{wildcards.outputdir}/{wildcards.name}",
            output_file = "{output.html}")'
        """

